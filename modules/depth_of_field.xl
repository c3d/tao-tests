// *****************************************************************************
// depth_of_field.xl                                               Tao3D project
// *****************************************************************************
//
// File description:
//
//
//
//
//
//
//
//
// *****************************************************************************
// This software is licensed under the GNU General Public License v3
// (C) 2012, Baptiste Soulisse <baptiste.soulisse@taodyne.com>
// (C) 2019, Christophe de Dinechin <christophe@dinechin.org>
// (C) 2012, Jérôme Forissier <jerome@taodyne.com>
// *****************************************************************************
// This file is part of Tao3D
//
// Tao3D is free software: you can r redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// Tao3D is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with Tao3D, in a file named COPYING.
// If not, see <https://www.gnu.org/licenses/>.
// *****************************************************************************
import DepthOfField

capture_page "depth_of_field",
    set_focus_distance
    color "white"
    dof_frame_texture window_width, window_height,
        balls
    rectangle 0, 0, window_width, window_height

balls ->
    light 0
    light_position 300, 500, 500
    locally
        translatex 120
        rotatex 5
        rotatey -20
        ball 5
    translate -120, 0, -600
    locally
        rotatex 10
        rotatey 15
        ball 2

ball N ->
    locally
        S -> 400
        color "white"
        locally
            light 0, false
            color 1, 1, 1, 1
            rectangle 0, 0, 220, 220
        frame_texture 400, 400,
            clear_color 1, 1, 1, 1
            scale 0.5, 1.0, 1.0
            if N = 5 then
                color "orange"
            else
                color "darkblue"
            rectangle 0, 0, 400, 400
            color "white"
            R -> S*25%
            ellipse 0, 0, R, R
            text_box 0, 0, R, R,
                align_center
                vertical_align_center
                color "black"
                font "Ubuntu", 72
                text text N
        texture_wrap true, true
        sphere 0, 0, 0, 200

set_focus_distance ->
    Debug -> 0
    // Set Debug := 1 to show focus hints and control focus with the mouse
    Debug := 0
    Near -> 1.94
    Far -> 2.34
    DOF_AUTOFOCUS := 0
    if Debug = 1 then
        DOF_SHOWFOCUS := 1
        DOF_FOCAL_DEPTH := 1.8 + 0.6*(1 - screen_mouse_y/window_height)
        writeln DOF_FOCAL_DEPTH
    else
        DOF_SHOWFOCUS := 0
        DOF_FOCAL_DEPTH := Near + (Far - Near)
